require no.ssb.fdb:21 as fdb
create-dataset Dataset

//henter personer som bodde  i Trøndelag når de var 16 år
import fdb/NUDB_KOMM_16 as bosted16
generate fylke = substr(bosted16,1,2)
generate kommnr = substr(bosted16,1,4)
keep if fylke == '50'| fylke == '16'| fylke  == '17' | kommnr  == '1567'| kommnr  == '1571' 

// Legger til kjønn
import fdb/BEFOLKNING_KJOENN as kjønn

//henter Årstall første gang registrert i videregående utdanning
import fdb/NUDB_AAR_NY_I_VID_UTD as  var1
generate Startår_VGS = substr(var1,1,4)
destring Startår_VGS

tabulate Startår_VGS  kjønn if Startår_VGS >= 2010, missing

//legger til utdanningsnivå - ikke fullført VGS, fullført vgs, fullført vgs og UH 
import fdb/NUDB_BU 2021-08-01 as utd
generate utd1 = substr(utd,1,1)
//lager kategorier
generate utdniv = '1'
replace utdniv = '2' if utd1 == '3' | utd1 == '4' | utd1 == '5'
replace utdniv = '3' if utd1 == '6' | utd1 == '7' | utd1 == '8' 
define-labels utdnivtxt '2'  'Fullført videregående ' '3' 'Universitets og høgskole utdanning'  '1' 'Ikke fullført videregående'
assign-labels utdniv utdnivtxt

generate fulldummy = '1'
replace fulldummy = '2' if utd1 == '3' | utd1 == '4' | utd1 == '5' |utd1 == '6' | utd1 == '7' | utd1 == '8' 
define-labels fulldummytxt '2'  'Fullført videregående '  '1' 'Ikke fullført videregående'
assign-labels fulldummy fulldummytxt

tabulate Startår_VGS utdniv if Startår_VGS >= 2000, missing rowpct freq 
tabulate Startår_VGS utdniv if Startår_VGS >= 2000, missing

keep if Startår_VGS == 2010
tabulate utdniv kjønn, missing colpct freq
tabulate fulldummy kjønn, missing colpct freq

//henter alder 
import fdb/BEFOLKNING_FOEDSELS_AAR_MND as faarmnd
generate alder = 2010 - int (faarmnd/100)
summarize alder
generate aldersgruppe = 'Ukjent '
replace aldersgruppe = '15 år eller yngere' if alder <= 15
replace aldersgruppe = '16 år' if alder == 16
replace aldersgruppe = '17 år' if alder == 17
replace aldersgruppe = '18 år' if alder == 18
replace aldersgruppe = '19 år eller eldre' if alder >= 19
tabulate aldersgruppe

//legger til grunnskolepoeng
// kun personer som har karakter i minst 8 av 11 fag får grunnskolepoeng
import fdb/NUDB_KURS_GRPOENG as Gpoeng
generate Grunnpoeng = '0 eller manglende opplysninger'
replace Grunnpoeng = '1-10' if Gpoeng > 1
replace Grunnpoeng = '10-30' if Gpoeng > 10
replace Grunnpoeng = '30-40' if Gpoeng > 30
replace Grunnpoeng = '40-50' if Gpoeng > 40
replace Grunnpoeng = '50-60' if Gpoeng > 50
replace Grunnpoeng = 'Over 60' if Gpoeng > 60

summarize Gpoeng

tabulate fulldummy Grunnpoeng, missing colpct freq
tabulate fulldummy Grunnpoeng, missing

import fdb/NUDB_SOSBAK as Sosialbakgrunn
generate Bkode = '9 Uoppgitt' 
replace Bkode = '4 Lang høyere' if Sosialbakgrunn == '1'
replace Bkode = '3 Kort høyere'  if Sosialbakgrunn == '2'
replace Bkode = '2 Videregående'   if Sosialbakgrunn == '3'
replace Bkode = '1 Grunnskole'   if Sosialbakgrunn == '4'

tabulate Sosialbakgrunn fulldummy, missing rowpct freq
tabulate Sosialbakgrunn fulldummy, missing

tabulate Sosialbakgrunn Grunnpoeng, missing
sankey Bkode Grunnpoeng

tabulate  Sosialbakgrunn fulldummy, summarize(Gpoeng)

//Henter personer som tar utdanning per 1. oktober i år x, og kobler dette på persondatasettet. 
create-dataset kursdata
import fdb/NUDB_KURS_NUS 2010-10-01 as kurstype
import fdb/NUDB_KURS_FNR as fnr
tabulate kurstype
//beholder de som er i videregående utdanning per 1. oktober i år x, - tenker å bruke denne for å skille mellom de som gikk allmen/yrkesfag 
destring kurstype
keep if kurstype >= 301106 & kurstype < 500000
tabulate kurstype

//lager allmen /ikke allmen
generate kurstype2 = substr(kurstype,1,2)
generate kurstype3 = substr(kurstype,1,3)
generate  allmen = 'Nei'
//ta utsjekk om 32-33 og 42-43 skal være med eller ikke
replace allmen = 'ja' if kurstype2 == '30' | kurstype3 == '315' | kurstype2 == '33' | kurstype2 == '40' | kurstype3 == '415' 

//FILLTER ALLMEN
//keep if allmen == 'ja'

collapse (count) kurstype, by(fnr)
rename kurstype ant_kurs
merge ant_kurs into Dataset

use Dataset

//test antall videregående kurs vs Trøndere som startet VGS i år x
tabulate Startår_VGS ant_kurs, missing
tabulate utdniv ant_kurs, missing

//keep if ant_kurs == 1 

//tabulerer utdanningsnivå på nytt
tabulate utdniv kjønn, missing colpct freq
tabulate utdniv kjønn, missing rowpct freq

//henter Årstall første gang fullført treårig utdanning 
import fdb/NUDB_AAR_FORSTE_FULLF_VS as  var2
generate Fullført_VGS = substr(var2,1,4)
destring Fullført_VGS
//keep if Fullørt_VGS> 2015 

tabulate  Startår_VGS Fullført_VGS, missing
tabulate Fullført_VGS kjønn, missing colpct freq
tabulate Fullført_VGS utdniv, missing colpct freq
tabulate Fullført_VGS, missing 

//første gang fullført treårig utdanning under lov om videregående opplæring
import fdb/NUDB_AAR_FORSTE_FULLF_VS_LOV as var2_1
generate Fullført_VGS_1 = substr(var2_1,1,4)
destring Fullført_VGS_1

tabulate  Fullført_VGS_1 Fullført_VGS, missing

//henter fullføringsår på allemen fag og yrkesfag
import fdb/NUDB_AAR_FORSTE_FULLF_VSY as  var2_2
generate Fullført_VGS_Y = substr(var2_2,1,4)
destring Fullført_VGS_Y 

tabulate Fullført_VGS_Y, missing

import fdb/NUDB_AAR_FORSTE_FULLF_VSA as  var2_3
generate Fullført_VGS_A = substr(var2_3,1,4)
destring Fullført_VGS_A 

tabulate Fullført_VGS_A, missing
tabulate Fullført_VGS_Y Fullført_VGS_A, missing

import fdb/NUDB_SEMESTER_FFF_VS as semester
tabulate semester utdniv, missing colpct freq

//starter å ta ut data om hva de driver med nå...

//stauskode til fødselsnummer- for å se hvor mange som har død eller flyttet ut
import fdb/BEFOLKNING_STATUSKODE 2021-01-01 as status

tabulate utdniv status, missing

//arbeidstatus
import fdb/REGSYS_ARB_ARBMARK_STATUS 2021-11-16 as yrkstat
tabulate yrkstat utdniv, missing colpct freq
tabulate yrkstat utdniv, missing rowpct freq
tabulate yrkstat utdniv, missing 

//henter arbeidstid
import fdb/REGSYS_ARB_ARBEIDSTID 2021-11-16 as atid
summarize atid  
histogram atid

tabulate yrkstat utdniv, summarize(atid)
tabulate yrkstat fulldummy, summarize(atid)

//Gruppere atid i  intervaller
generate atidint = 'ikke lønnstaker'
replace atidint = '0-15' if atid > 0
replace atidint = '15-20' if atid > 15
replace atidint = '20-25' if atid > 20
replace atidint = '25-30' if atid > 25
replace atidint = '30-35' if atid > 30
replace atidint = '35-37.5' if atid > 35
replace atidint = 'Over 37.5' if atid > 37.50

tabulate atidint utdniv if yrkstat =='1', missing 
tabulate atidint fulldummy if yrkstat =='1', missing 

//legger til inntekt 
import fdb/INNTEKT_WSAMINNT 2021-12-31 as inntekt

summarize inntekt
tabulate  yrkstat utdniv, summarize(inntekt)
tabulate  yrkstat fulldummy, summarize(inntekt)

//lager dummy på sysselsatt ikke sysselsatt
generate sysselsatt = 'Ikke sysselsatt'
replace sysselsatt = 'Sysselsatt' if yrkstat == '1'| yrkstat == '2'

tabulate  sysselsatt utdniv, summarize(inntekt)
tabulate  sysselsatt utdniv, missing colpct freq

tabulate  sysselsatt fulldummy, summarize(inntekt)
tabulate  sysselsatt fulldummy, missing colpct freq

histogram inntekt if fulldummy == '1'
tabulate atidint fulldummy if yrkstat =='1', summarize(inntekt) 

//henter yrke per 4. kvartal 2021
import fdb/REGSYS_ARB_YRKE_STYRK08 2021-11-16 as yrke

tabulate yrke utdniv if yrkstat =='1', missing
tabulate yrke fulldummy if yrkstat =='1', missing

import fdb/UFOERP2011FDT_GRAD 2021-11-30 as grad
tabulate grad utdniv, missing colpct freq
generate ufør = 'ikke  ufør'
replace ufør = 'ufør' if grad > 0

tabulate ufør fulldummy, missing colpct freq

import fdb/SOSHJLPZFDT_BIDB 2021-12-31 as sosialhjelp
summarize sosialhjelp

tabulate utdniv, summarize(sosialhjelp)

generate sosial = 'Nei'
replace sosial = 'ja' if sosialhjelp >= 1

tabulate utdniv sosial, missing
tabulate sosial grad, missing
tabulate utdniv sosial grad, missing

tabulate fulldummy sosial ufør, missing

//henter eie/leie status og trangbodhet
import fdb/BOFORHOLD_EIE_LEIE 2022-01-01 as eie_leie
tabulate fulldummy eie_leie,missing

import fdb/BOFORHOLD_TRANGBODD 2022-01-01 as trang
tabulate fulldummy trang,missing

//legger til familietype
import fdb/BEFOLKNING_REGSTAT_FAMTYP 2022-01-01 as familie
generate Familkat  = substr(familie,1,1)
generate Familiegruppe = '9- ukjent'
replace Familiegruppe ='1- Enpersonsfamilie' if Familkat == '1'
replace Familiegruppe = '2 - Familier med barn under 18 år' if Familkat == '2'
replace Familiegruppe = '3 - Andre familier' if Familkat == '3'

tabulate familie kjønn, missing
tabulate familie fulldummy, missing

tabulate Familiegruppe kjønn, missing

tabulate Familiegruppe fulldummy, missing
tabulate kjønn Familiegruppe fulldummy, missing

//starter å se på bosted 
tabulate bosted16, missing 
generate Trondheim2010 = '02- Resten av Trøndelag'
replace Trondheim2010 = '01- Trondheim' if bosted16 == '1601' | bosted16  == '1662'

tabulate Trondheim2010 fulldummy, missing rowpct colpct freq

//henter bosted 2010 for å se hvor mange som ikke bodde i Trøndelag det året de startet vgs
import fdb/BOSATTEFDT_BOSTED 2010-11-01 as bosted
generate fylke2 = substr(bosted,1,2)
generate kommnr2 = substr(bosted,1,4)

generate Trøndelag2010 = '02 - ikke Trøndelag'
replace Trøndelag2010 = '01 -Trøndelag' if fylke2 == '50'| fylke2 == '16'| fylke2  == '17'  | kommnr2  == '1567'| kommnr2  == '1571'
tabulate Trøndelag2010 aldersgruppe

//henter bosted 2022
//henter bostedskommuen og lager fylker
import fdb/BEFOLKNING_KOMMNR_FORMELL 2022-01-01 as bosted2022
generate BFylke2022 = substr(bosted2022,1,2)
define-labels botxt '03' 'Oslo' '11' 'Rogaland' '15' 'Møre og Romsdal' '18'	'Nordland''30''Viken' '34''Innlandet''38' 'Vestfold og Telemark''42'	'Agder''46'	'Vestland' '50''Trøndelag'	'54''Troms og Finnmark'
assign-labels BFylke2022 botxt

tabulate BFylke2022 fulldummy, missing
